group 'mint'

//noinspection GroovyAssignabilityCheck
dependencies {
    compile jackson_csv, jcabi, guava

    testCompile junit, mockito
}


jar.baseName = "loader"

jar {
    doFirst {
        from {
            configurations.compile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }

        manifest {
            attributes(
                    "Main-Class": "uk.gov.admin.LoaderApplication",
                    "Class-Path": configurations.runtime.findAll { !it.directory }.collect { it.name }.join(' ')
            )
        }
    }
}

task bulkLoad(type: JavaExec) {
    def properties = project.getProperties()
    def datafile = properties.get("datafile")
    def mintUrl = properties.get("mintUrl")
    def type = properties.get("type") == null ? "jsonl" : properties.get("type")

    main = 'uk.gov.admin.LoaderApplication'
    classpath = sourceSets.main.runtimeClasspath
    args = ["--datafile=${normalizeDataFilePath(datafile)}", "--mintUrl=$mintUrl", "--type=$type"]

    doFirst{
        if (datafile == null || mintUrl == null) {
            throw new AssertionError("Missing required parameters -Pdatafile and -PmintUrl.\n\nUsage: gradle bulkLoad -PmintUrl=<mintUrl> -Pdatafile=<loadfile.json> [-Ptype=jsonl|tsv|csv]")
        }
    }
}

String normalizeDataFilePath(String dataFile) {
    dataFile?.startsWith("http://") || dataFile?.startsWith("https://") ?
            dataFile :
            new File(dataFile.toString()).absolutePath
}
