group 'mint'

//noinspection GroovyAssignabilityCheck
dependencies {
    compile jcabi, jOptSimple, javaslang, univocity

    testCompile junit, mockito
}


jar.baseName = "loader"

jar {
    doFirst {
        from {
            configurations.compile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }

        manifest {
            attributes(
                    "Main-Class": "uk.gov.admin.Loader",
                    "Class-Path": configurations.runtime.findAll { !it.directory }.collect { it.name }.join(' ')
            )
        }
    }
}

task bulkLoad(type: JavaExec)  {
    def properties = project.getProperties()
    def datafile = properties.get("datafile")
    def configfile = properties.get("configfile")
    def type = properties.get("type") == null ? "jsonl" : properties.get("type")

    main = 'uk.gov.admin.Loader'
    classpath = sourceSets.main.runtimeClasspath
    args = ["--datafile=$datafile", "--configfile=$configfile", "--type=$type"]

    doFirst {
        if (datafile == null || configfile == null) {
            throw new AssertionError("Missing required parameters -Pdatafile and -Pconfigfile.\n\nUsage: gradle bulkLoad -Pconfigfile=<config.properties> -Pdatafile=<loadfile.json> [-Ptype=jsonl|tsv|csv]")
        }
    }
}

task stage(dependsOn: ['assemble'])
